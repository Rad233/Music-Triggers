//file:noinspection GroovyUnusedAssignment
//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'gradle.plugin.com.github.johnrengelman', name: 'shadow', version: '7.1.2'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.16.5-6.0-EX'
group = 'mods.thecomputerizer.musictriggers'
archivesBaseName = 'musictriggers'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                musictriggers {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                musictriggers {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'musictriggers', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                musictriggers {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = uri("https://m2.dv8tion.net/releases")
    }
    maven {
        url = uri("https://jitpack.io")
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    shade
    implementation.extendsFrom(shade)
    shadow.extendsFrom shade
}

dependencies {
    //minecraft
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.29'

    //external dependencies
    shade("io.github.kostaskougios:cloning:1.10.3")
    shade("com.moandjiezana.toml:toml4j:0.7.2") { exclude group: 'com.google.code.gson', module: 'gson' }
    shade("com.github.walkyst:lavaplayer-fork:1.3.97.1")
    shade("com.sedmelluq:lava-common:1.1.2")
    shade("org.apache.commons:commons-lang3:3.12.0") //this has to be shaded and relocated separately

    //mods
    implementation fg.deobf("curse.maven:game-stages-268655:3480161")
    implementation fg.deobf("curse.maven:bookshelf-228525:3474499")
    implementation fg.deobf("curse.maven:enhanced-celestials-438447:3532022")
    implementation fg.deobf("curse.maven:configured-457570:3546348")
    implementation fg.deobf("curse.maven:atomicstrykers-infernal-mobs-227875:3048405")
    implementation fg.deobf("curse.maven:dynamic-surroundings-238891:3502320")
    implementation fg.deobf("curse.maven:better-weather-400714:3420517")
    implementation fg.deobf("curse.maven:serene-seasons-291874:3510900")
    implementation fg.deobf("curse.maven:champions-307152:3742182")
    implementation fg.deobf("curse.maven:blue-skies-312918:3411324")
    implementation fg.deobf("curse.maven:jei-238222:3488178")
}

shadowJar {
    //necessary relocation
    relocate 'org.slf4j', 'mods.thecomputerizer.musictriggers.libraries.sljf4'
    relocate 'org.apache.commons', 'mods.thecomputerizer.musictriggers.libraries.apache.commons'
    relocate 'org.apache.http', 'mods.thecomputerizer.musictriggers.libraries.apache.http'
    configurations = [project.configurations.shadow]
    classifier ''
}


reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

mixin {
    add sourceSets.main, "musictriggers.refmap.json"
}

jar {
    manifest {
        attributes([
            "Specification-Title": "musictriggers",
            "Specification-Vendor": "musictriggerssareus",
            "Specification-Version": "6.0-EX",
            "Implementation-Title": project.name,
            "Implementation-Version": "6.0-EX",
            "Implementation-Vendor" :"musictriggers",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "MixinConfigs": "musictriggers.mixin.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
tasks.assemble.dependsOn shadowJar
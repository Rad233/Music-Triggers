//file:noinspection GroovyUnusedAssignment
//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name 'ParchmentMC'
		url = uri('https://maven.parchmentmc.org')
	}
	maven {
		url = 'https://www.cursemaven.com'
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = 'Modrinth'
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		url = uri('https://m2.dv8tion.net/releases')
	}
	maven {
		url = uri('https://jitpack.io')
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.19.2:2022.11.27@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//3rd party dependencies
	include (implementation("io.github.kostaskougios:cloning:1.10.3"))
	include (implementation('com.moandjiezana.toml:toml4j:0.7.2') {
		exclude group: 'com.google.code.gson', module: 'gson'
	})
	include (implementation('com.github.walkyst:lavaplayer-fork:1.3.97.1'))

	//3rd party recursive dependencies
	include (implementation('com.sedmelluq:lava-common:1.1.2'))
	include (implementation('com.sedmelluq:lavaplayer-natives:1.3.14'))
	include (implementation('org.objenesis:objenesis:3.2'))
	include (implementation('com.fasterxml.jackson.core:jackson-core:2.13.3'))
	include (implementation('com.fasterxml.jackson.core:jackson-databind:2.13.3'))
	include (implementation('com.fasterxml.jackson.core:jackson-annotations:2.13.3'))
	include (implementation('org.apache.commons:commons-lang3:3.12.0'))

	//mods
	modImplementation 'maven.modrinth:the-impossible-library:1.19.2-fabric-0.2.2'
	modImplementation 'curse.maven:cloth-config-319057:3641132'
	modImplementation 'curse.maven:modmenu-308702:3570652'
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

loom {
	accessWidenerPath = file("src/main/resources/musictriggers.accesswidener")
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
